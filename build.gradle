plugins {
    id 'org.ajoberstar.reckon' version '0.9.0'
    id 'org.ajoberstar.grgit' version '3.0.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'java-library'

group 'net.minecraftforge'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
}

jacoco {
    toolVersion = "0.8.2"
}

reckon {
    scopeFromProp()
    stageFromProp('milestone', 'rc', 'final')
}

reckonTagCreate.dependsOn check

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

test {
    useJUnitPlatform()
}

ext.sharedManifest = manifest {
    attributes(["Specification-Title"     : "forgespi",
                "Specification-Vendor"    : "Forge",
                "Specification-Version"   : "1", // Currently version 1 of the forgespi specification
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}+${System.getenv("BUILD_NUMBER") ?: 0}+${grgit.head().abbreviatedId}",
                "Implementation-Vendor"   : "Forge",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Git-Commit"              : grgit.head().abbreviatedId,
                "Git-Branch"              : grgit.branch.current().getName()],
            "net/minecraftforge/forgespi/")
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


dependencies {
    implementation("cpw.mods:modlauncher:0.10.+")
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    implementation("org.ow2.asm:asm:6.2")
    implementation("org.ow2.asm:asm-commons:6.2")
    implementation("org.ow2.asm:asm-tree:6.2")
    implementation("org.apache.logging.log4j:log4j-api:2.11.+")
    implementation("org.apache.maven:maven-artifact:3.6.0")
    implementation("com.electronwill.night-config:core:3.4.2")
    implementation("com.electronwill.night-config:toml:3.4.2")


    testRuntime("org.apache.logging.log4j:log4j-core:2.11.+")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.4.+")
    testImplementation("org.powermock:powermock-core:2.0.+")
    testImplementation("org.hamcrest:hamcrest-core:2.1+")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.4.+")
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                name = 'Forge Service Provider Interface'
                description = 'API for services which can plug into forge, or which are used by forge in other places'
                url = 'https://github.com/MinecraftForge/forgespi'
                scm {
                    url = 'https://github.com/MinecraftForge/forgespi'
                    connection = 'scm:git:git://github.com/MinecraftForge/forgespi.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/forgespi.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/forgespi/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.properties.forgeMavenUser?:'fake'
                password project.properties.forgeMavenPassword?:'news'
            }
            url "http://files.minecraftforge.net/maven/manage/upload"
        }
    }
}